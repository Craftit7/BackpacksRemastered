/*
 * BackpacksRemastered - remastered version of the popular Backpacks plugin
 * Copyright (C) 2019 Division Industries LLC
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */


import com.divisionind.i18n.DiscoveredString
import com.divisionind.i18n.ExtractorPlugin
import com.divisionind.i18n.GoogleTranslator
import com.divisionind.i18n.api.StringDecider
import com.divisionind.i18n.api.FileScanUtils
import com.divisionind.i18n.api.corrector.RegexCorrectorMatcher
import org.apache.tools.ant.filters.ReplaceTokens
import java.lang.reflect.Method
import org.gradle.internal.os.OperatingSystem
//import com.backblaze.b2.client.B2StorageClient
//import com.backblaze.b2.client.B2StorageClientFactory
//import com.backblaze.b2.client.structures.B2UploadFileRequest

import java.util.regex.Pattern

buildscript {
    repositories {
        maven {
            url 'https://raw.githubusercontent.com/divisionind/maven/repo'
            //url uri('../maven') // local repo for testing
        }
    }
    dependencies {
        classpath group: 'com.divisionind',
                name: 'i18nExtractor',
                version: '2019.0.8'

        // https://mvnrepository.com/artifact/com.backblaze.b2/b2-sdk-core
        //classpath group: 'com.backblaze.b2', name: 'b2-sdk-core', version: '3.1.0'

        // https://mvnrepository.com/artifact/com.backblaze.b2/b2-sdk-httpclient
        //classpath group: 'com.backblaze.b2', name: 'b2-sdk-httpclient', version: '3.1.0'
    }
}

plugins {
    id 'java'

    id "com.github.breadmoirai.github-release" version "2.2.9"
}

apply plugin: ExtractorPlugin

def baseVersion = "2019.1.4"
def beta = true
def uploadBetas = false

def spigotVersion = "1.14.2"
def githubOwner = "divisionind"
def runningServerPluginsDir = System.env.DEV_SERVER
def wslCommand = "kali"

group 'com.divisionind'
sourceCompatibility = 1.8

// returns hash of the current commit
def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

// returns what commit this is in the entire history of the repo
def getGitCommitNum = { shash ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--count', shash
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

// gets the changes sense the specified short hash
def getGitChanges = { shash ->
    def stdout = new ByteArrayOutputStream()
    exec { // if this fails, try changing %h and %s to separate arguments
        int lastReleaseInt
        try {
            lastReleaseInt = Integer.parseInt(getGitCommitNum(shash))
        } catch (NumberFormatException e) {
            lastReleaseInt = 0
        }
        commandLine 'git', 'log', '--pretty=format:* %h %s', '--max-count', "${Integer.parseInt(getGitCommitNum('HEAD'))-Integer.parseInt(getGitCommitNum(shash))}"
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

// what format the version info should take
version baseVersion + (beta ? "-beta.${getGitCommitNum('HEAD')}" : "")

// must be used after githubRelease, could be used for providing changelog to other destinations than github
def lastRelease

// generates sources by replacing these strings (surrounded by @, e.g. @DivisionVersion@)
task generateSources(type: Copy) {
    from 'src/main/java'
    into "$buildDir/generated-src"

    filter(ReplaceTokens, tokens: [
            'DivisionVersion': project.version,
            'DivisionGitHash': getGitHash(),
            'DivisionGitComm': getGitCommitNum('HEAD')
    ])
}

// generates resources by replacing these strings (surrounded by @, e.g. @DivisionVersion@)
task generateResources(type: Copy) {
    from 'src/main/resources'
    into "$buildDir/generated-resources"

    filter(ReplaceTokens, tokens: [
            'DivisionVersion': project.version,
            'DivisionGitHash': getGitHash(),
            'DivisionGitComm': getGitCommitNum('HEAD')
    ])
}

internationalize {
    sourcesDir file("$buildDir/generated-src")
    sourceResources file("$buildDir/generated-resources")
    bundleInitializerClasspath 'com.divisionind.bprm.Backpacks'
    cacheDirectory file("$projectDir/.i18nExtractor")

    File commandsFile = file("$sourcesDir/com/divisionind/bprm/commands")
    Pattern colorPat = Pattern.compile("(?i)" + String.valueOf('&') + "[0-9A-FK-OR]")
    stringDecider new StringDecider() {
        @Override
        boolean shouldAdd(DiscoveredString st) {
            // exclude the names of backpacks from this (for now)
            if (st.value == '&9Backpacks &7&l>>&r ' || st.value == '&aLinked' || st.value == 'Players=%s&BackpacksVersion=%s') {
                return false
            }

            // return yes for any colored string
            if (colorPat.matcher(st.value).results().count() > 0) return true

            // these string values are not colored and would not be picked up otherwise
            if (st.locationFile.getParentFile().equals(commandsFile)) {
                String prevLine = FileScanUtils.getLine(st.locationFile, st.line)
                return prevLine.contains("String desc()")
            }
        }
    }
    
    // serializes color codes so they dont get messed with by the translator
    translator = new GoogleTranslator(69, 10, new RegexCorrectorMatcher(colorPat), 
            new RegexCorrectorMatcher(Pattern.compile("0x%02x", Pattern.LITERAL)),
            new RegexCorrectorMatcher(Pattern.compile("%.2f", Pattern.LITERAL)))

    langs('es', 'it', 'fr', 'zh-CN')
}
internationalize.dependsOn generateSources, generateResources
compileJava.dependsOn internationalize
processResources.dependsOn internationalize

compileJava.setSource("$buildDir/internationalized-src")
compileJava.dependsOn internationalize
processResources.from("$buildDir/internationalized-resources")
processResources.dependsOn internationalize

task makeJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': version
    }
    baseName = project.name
    from { configurations.compile.filter{ it.getName().endsWith(".jar") }.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task pack(dependsOn: ['clean', 'makeJar'])
makeJar.mustRunAfter clean

// whether or not to upload a release
boolean uploadRelease = !(beta && !uploadBetas)

githubRelease {
    token "${System.env.GITHUB_TOKEN}"
    owner githubOwner
    repo project.name
    tagName "v${project.version}"
    targetCommitish "master"
    releaseName "v${project.version}"
    draft false
    prerelease beta
    releaseAssets "build/libs/${project.name}-${project.version}.jar"
    apiEndpoint "https://api.github.com"

    // gets the last released commit from releaseplugin to calculate change log on its own (more control) TODO do this without reflection (because gradle will deny it in future versions)
    Class c = Class.forName("com.github.breadmoirai.githubreleaseplugin.ChangeLogSupplier")
    Method m = c.getDeclaredMethod("getLastReleaseCommit")
    m.setAccessible(true)
    lastRelease = m.invoke(changelog())

    if (uploadRelease) {
        body "${getGitChanges(lastRelease)}"
    }
}

// a dummy task so that the githubRelease task may be disable based on certain parameters
task uploadArtifacts {}

if (uploadRelease) {
    println "Version qualified for a release."
    uploadArtifacts.finalizedBy tasks.githubRelease
}

/* may add beta uploading from travis to my website, idk how many people would be interested in that though
  task betaUpload {
      B2StorageClient client = B2StorageClientFactory.createDefaultFactory().create(System.env.B2_KEY_ID, System.env.B2_KEY, "Mozilla/5.0 (Windows NT 10.0; Win64; x64; rv:69.0) Gecko/20100101 Firefox/69.0")
      B2UploadFileRequest request = B2UploadFileRequest.builder("", "", "") // https://github.com/Backblaze/b2-sdk-java
      client.uploadSmallFile()
  }

  if (System.env.B2_KEY != null && !uploadRelease) {
      println "Version qualified for beta build upload."
      uploadArtifacts.finalizedBy betaUpload
  } */

def sourceJar = "build/libs/${project.name}-${project.version}.jar"
def dstJar = "${runningServerPluginsDir}/${project.name}.jar"

task update {
    doLast {
        println "moving $sourceJar => $dstJar"
        OperatingSystem os = OperatingSystem.current()
        if (os.isWindows()) {
            exec {
                commandLine wslCommand, 'run', "mv $sourceJar $dstJar"
            }
        } else
        if (os.isLinux()) {
            exec {
                commandLine 'mv', sourceJar, dstJar
            }
        } else {
            println 'This OS does not support real-time plugin updating.'
            return
        }
        println 'Updated plugin on running server. Reload it now to see the changes.'
    }
}
update.dependsOn pack

repositories {
    mavenCentral()

    maven { url 'https://raw.githubusercontent.com/divisionind/maven/repo' }
}

dependencies {
    compileOnly group: 'org.spigotmc', name: 'spigot-full', version: spigotVersion

    testCompile group: 'junit', name: 'junit', version: '4.12'
}
